/**
 * =====================================
 * UISortPortalToggle ( field ; object )
 *
 * PURPOSE:
 *		Assign a reserved set of global variables to values used
 *		by a sorting script (see UISortPortalDirection and UISortPortalIndicator)
 *		Reserved global variables are classed to $$SORT.PORTAL.[object].* where
 *		[id] is the internal ID of the current layout.
 *
 * RETURNS:
 *		(boolean) The result of a successful assignment of variables
 *
 * PARAMETERS:
 *		field: A field reference
 *
 * EXAMPLES:
 *		When used as a script parameter on a button defined to run a sort script
 *		UISortPortalToggle ( FunctionalArea » Tablename::fieldName ; "portal.name" ) // = Assignment of $$SORT.PORTAL.[object].* variables
 *
 * DEPENDENCIES:
 *		#Assign()
 *
 * RELEASE:
 *		2011-04-20
 *
 * REFERENCES:
 *		none
 * =====================================
 */

Let ( [
	~fieldName = GetFieldName ( field );
	~sortVarPrefix = "$$SORT.PORTAL." & Upper ( object );
	~sortVarField = ~sortVarPrefix & ".FIELD";
	~sortVarDirection = ~sortVarPrefix & ".DIRECTION";
	~sortCurrentField = GetAsText ( Evaluate ( ~sortVarField ) );
	~isNewFieldSelection = ~sortCurrentField ≠ ~fieldName;
	~sortNewDirection = If ( ~isNewFieldSelection ; 1 ; Evaluate ( ~sortVarDirection ) xor True );
	~variables = List (
					~sortVarField & " = " & Quote ( ~fieldName ) & ";";
					~sortVarDirection & " = " & ~sortNewDirection;
				)
];
	If ( #Assign ( ~variables ) ; Evaluate ( ~sortVarField ) ; False )
)